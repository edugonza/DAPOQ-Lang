plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

wrapper.gradleVersion = '4.6'

version = '1.0-alpha'
group = 'com.github.edugonza'

println name+" v"+version

ext {
	rapidprom_version = '4.0.2'
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
	ivy {
		url "https://github.com/rapidprom/rapidprom-libraries/raw/${rapidprom_version}/prom/"
		layout "pattern", {
			artifact "[module]-[revision]/[module]-[revision].[ext]"
			artifact "[module]-[revision]/[artifact].[ext]"
			ivy "[module]-[revision]/ivy-[module]-[revision].xml"
		}
	}
	ivy {
		url "https://github.com/rapidprom/rapidprom-libraries/raw/${rapidprom_version}/thirdparty/lib/"
		layout "pattern", {
			artifact "[module]-[revision]/[module]-[revision].[ext]"
			ivy "[module]-[revision]/ivy-[module]-[revision].xml"
		}
	}
	ivy {
		url "https://github.com/rapidprom/rapidprom-libraries/raw/${rapidprom_version}/thirdparty/resource/"
		layout "pattern", {
			artifact "[module]-[revision]/[module]-[revision].[ext]"
			ivy "[module]-[revision]/ivy-[module]-[revision].xml"
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs = []
		}
		groovy{
			srcDirs = ['src/']
		}
		resources {
			srcDirs = ['resources/']
		}
	}
	test {
		java {
			srcDirs = ['test/src/']
		}
		resources {
			srcDirs = ['test/resources/']
		}
	}
}

task benchmark(dependsOn: 'testClasses', type: JavaExec) {
	main = "org.processmining.database.metamodel.dapoql.test.benchmark.RunBenchmarkTest"
	classpath = sourceSets.test.runtimeClasspath
	classpath += sourceSets.main.runtimeClasspath
	enableAssertions = true
}

jar {
	dependsOn javadoc
	from sourceSets.main.allSource
	from javadoc.destinationDir
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		mavenLocal()
	}
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

dependencies {
	compile  'org.rapidprom:GraphViz:6.7.185'
	compile  'commons-io:commons-io:2.6'
	compile  'org.apache.commons:commons-compress:1.16.1'
//	compile  'com.github.edugonza:OpenSLEX:0.9'
	compile  'com.github.edugonza:OpenSLEX:development-SNAPSHOT'
	compile  'org.codehaus.groovy:groovy-all:2.4.14'
	compile  'com.fifesoft:rsyntaxtextarea:2.6.1'
	compile  'com.fifesoft:autocomplete:2.6.1'
	compile  'com.fifesoft:languagesupport:2.6.0'
	testCompile 'junit:junit:4.12'
}

